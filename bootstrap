#!/bin/zsh

# Bootstrap a new machine.
#
# Arguments passed in are directly passed to `ansible-playbook`

DEBUG=

function print_bootstrap() {
  cat << 'EOF'
 _                 _       _
| |__   ___   ___ | |_ ___| |_ _ __ __ _ _ __
| '_ \ / _ \ / _ \| __/ __| __| '__/ _` | '_ \
| |_) | (_) | (_) | |_\__ \ |_| | | (_| | |_) |
|_.__/ \___/ \___/ \__|___/\__|_|  \__,_| .__/
                                        |_|
                             ___  ____
       _ __ ___   __ _  ___ / _ \/ ___|
      | '_ ` _ \ / _` |/ __| | | \___ \
      | | | | | | (_| | (__| |_| |___) |
      |_| |_| |_|\__,_|\___|\___/|____/

EOF
}

function print_done() {
  cat << 'EOF'

  Don't forget to start tmux and install tmux plugins.

  `tmux new-session -s setup`

  Then press `<C-a>L` to install plugins, the tpm package should
  already be installed.

  ✅ Done. Note some changes may require a reboot / logout to take effect.

EOF

}

function install_homebrew() {
  print "\n🍺 Checking for homebrew"
  [ -z "$(command -v brew)" ] || return 0
  /bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"
  (echo; echo 'eval "$(/opt/homebrew/bin/brew shellenv)"') >> "$HOME/.zprofile"
  eval "$(/opt/homebrew/bin/brew shellenv)"
}

function install_ansible() {
  print "📖 Checking for ansible"
  [ -z "$(command -v ansible)" ] && brew install ansible
}

function ensure_just() {
  print "🏛️Checking for just"
  [ -z "$(command -v just)" ] && brew install just
}

function run_playbook() {
  [ -n "$DEBUG" ] && \
    print "⚠️ Aborting because in debug mode" && \
    return 1

  just run-playbook "$@"

  print "\n✅ Done running playbook."
}

function main() {
  print_bootstrap
  install_homebrew
  install_ansible
  ensure_just

  print "\n🤖 Installing ansible-playbook requirements"
  just install-requirements

  print "\n📚 Running ansible-playbook with args: $@"
  [ -z "$DEBUG" ] && \
    print "⛔️ This may ask for your password several times during it's run."

  run_playbook "$@" || return 0 # this is if we're in debug mode, don't continue.

  print "🛠️Fixing permissions on homebrew share directory"
  just ensure-homebrew-permissions

  print "\n🔌 Installing neovim plugins, this may take a while"
  just install-nvim-plugins

  print_done

  print "🐚 Reloading shell."
  exec zsh -l
}

main "$@"

